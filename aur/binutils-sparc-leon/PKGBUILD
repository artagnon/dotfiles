# Maintainer: Ramkumar Ramachandra <artagnon@gmail.com>

_pkgname=binutils
_target="sparc-leon3-linux"
_sysroot="/opt/${_pkgname}-sparc-leon"

pkgname=${_pkgname}-sparc-leon
pkgver=2.24
pkgrel=1
pkgdesc="binutils for sparc-leon targets"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
depends=('zlib')
options=('!libtool' '!distcc' '!ccache' '!buildflags')
source=("ftp://ftp.gnu.org/gnu/binutils/${_pkgname}-${pkgver}.tar.bz2")
md5sums=('e0f71a7b2ddab0f8612336ac81d9636b')

build() {
	cd ${srcdir}/${_pkgname}-${pkgver}/

	./configure --prefix=${_sysroot}/usr \
		--with-sysroot=${_sysroot} \
		--target=${_target} \
		--with-cpu=v8 \
		--with-float=soft \
		--disable-nls \
		--disable-multilib

	make || return 1
}

package() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install || return 1

	install -dm755 ${pkgdir}/${_sysroot}/usr/include
	install -vm644 ${srcdir}/${_pkgname}-${pkgver}/include/libiberty.h ${pkgdir}${_sysroot}/usr/include/
	install -vm644 ${srcdir}/${_pkgname}-${pkgver}/include/demangle.h ${pkgdir}${_sysroot}/usr/include/

	msg "Cleaning-up cross compiler tree..."
	rm -rf ${pkgdir}/${_sysroot}/usr/share/{info,man}
	rm ${pkgdir}/${_sysroot}/usr/${_target}/bin/*

	msg "Creating out-of-path executables..."
	mkdir -p ${pkgdir}/${_sysroot}/usr/${_target}/bin/
	cd ${pkgdir}${_sysroot}/usr/${_target}/bin/
	for bin in ${pkgdir}${_sysroot}/usr/bin/${_target}-*; do
		bbin=`basename "$bin"`;
		ln -s ${_sysroot}/usr/bin/$bbin `echo "$bbin" | sed "s#^${_target}-##"`;
	done

	msg "Creating /usr/bin symlinks..."
	mkdir -p $pkgdir/usr/bin
	for bin in ${pkgdir}${_sysroot}/usr/bin/${_target}-*; do
		bbin=`basename "$bin"`;
		ln -s "${_sysroot}/usr/bin/${bbin}" "${pkgdir}/usr/bin/${bbin}";
	done
}
